plugins {
    id 'java'
}
apply from: 'version.gradle'

sourceCompatibility = '11'
targetCompatibility = '11'

tasks.register('uberJar', Jar) {
    manifest {
        attributes(
                'Multi-Release': 'true',
                'Main-Class': 'ru.aklementev.html.parser.App'
        )
    }
    archiveClassifier = 'uber'
    archivesBaseName = "app"

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

jar.enabled = false
build {
    dependsOn uberJar
}

group 'ru.aklementev.html.parser'
version '1.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation "org.junit.jupiter:junit-jupiter-api:${versions.jupiter}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${versions.jupiter}"
    implementation "org.apache.logging.log4j:log4j-api:${versions.log4j}"
    implementation "org.apache.logging.log4j:log4j-core:${versions.log4j}"
    implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${versions.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    implementation "com.google.inject:guice:${versions.guice}"

}

test {
    useJUnitPlatform()
}

